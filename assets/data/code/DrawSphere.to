//Draw a sphere inside the console.
//This code comes from the page ( http://www.rosettacode.org/wiki/Draw_a_sphere ) 
//and has been adapted in Toba language from the C code.
 
func:normalize(v){
	len = sqrt(pow(v[0],2) + pow(v[1],2) + pow(v[2],2))
	return( (v[0]/len, v[1]/len, v[2]/len) )
}
 
func:dot(x,y){
	_dot = 0
	d = x[0]*y[0] + x[1]*y[1] + x[2]*y[2]
	if( d < 0 ){ _dot = -d }
	return(_dot)
}
 
func:draw_sphere(r, k, ambient, light){
	shades = (".",":","!","*","o","e","&","#","%","@")
	i = for(floor(-r),ceil(r)+1,1){
		x = i + 0.5
		line = null()
 		j = for(floor(-2*r),ceil(2*r)+1,1){
			y = j/2 + 0.5
			if((x*x + y*y) <= r*r){
				vec = normalize((x,y,sqrt(r*r - x*x - y*y)))
				b = pow(dot(light,vec),k) + ambient
				intensity = (1-b)*(size(shades)-1)
				if (intensity < 0){intensity = 0}
				if (intensity > size(shades) - 1){
					intensity = size(shades) - 2
				}
				line = line $ shades[intensity]
			}
			else{
				line = line $ " " 
			}
		}		
		print(line)
	}
}

light = normalize((30,30,-50))
draw_sphere(20,4,0.1, light)
draw_sphere(10,2,0.4, light)
